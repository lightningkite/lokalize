buildscript {
    ext.repositoryName = project.name
    ext.versionsProperties = new Properties()
    file("versions.properties").withInputStream { versionsProperties.load(it) }

    repositories {
        mavenLocal()
        jcenter()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-dev' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versionsProperties.kotlin}"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
    }
}

apply plugin: 'kotlin-multiplatform'
apply plugin: 'maven-publish'

group 'com.lightningkite'
version versionsProperties.getProperty(repositoryName)


repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
    maven { url 'https://dl.bintray.com/lightningkite/com.lightningkite.krosslin' }
}

kotlin {
    targets {
        fromPreset(presets.jvm, 'jvm')
        fromPreset(presets.js, 'js')
        if (org.gradle.internal.os.OperatingSystem.current().isMacOsX()) {
            fromPreset(presets.iosX64, 'iosx64')
            fromPreset(presets.iosArm64, 'ios')
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                api 'org.jetbrains.kotlin:kotlin-stdlib-common'
                api "com.lightningkite:kommon-metadata:${versionsProperties.kommon}"
            }
        }
        commonTest {
            dependencies {
                api 'org.jetbrains.kotlin:kotlin-test-common'
                api 'org.jetbrains.kotlin:kotlin-test-annotations-common'
            }
        }
        jvmMain {
            dependencies {
                api 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
                api "com.lightningkite:kommon-jvm:${versionsProperties.kommon}"
            }
        }
        jvmTest {
            dependencies {
                api 'org.jetbrains.kotlin:kotlin-test'
                api 'org.jetbrains.kotlin:kotlin-test-junit'
            }
        }
        jsMain {
            dependencies {
                api 'org.jetbrains.kotlin:kotlin-stdlib-js'
                api "com.lightningkite:kommon-js:${versionsProperties.kommon}"
            }
        }
        jsTest {
            dependencies {
                api 'org.jetbrains.kotlin:kotlin-test-js'
            }
        }

        if (org.gradle.internal.os.OperatingSystem.current().isMacOsX()) {
            iosMain {
                dependencies {
                    api "com.lightningkite:kommon-ios:${versionsProperties.kommon}"
                }
            }
            iosTest {
                dependencies {
                }
            }
            iosx64Main {
                kotlin.srcDir('src/iosMain/kotlin')
                dependencies {
                    api "com.lightningkite:kommon-iosx64:${versionsProperties.kommon}"
                }
            }
            iosx64Test {
                kotlin.srcDir('src/iosTest/kotlin')
                dependencies {
                }
            }
        }
    }
}

tasks.withType(compileKotlinJs.getClass()) {
    kotlinOptions {
        moduleKind = "umd"
        sourceMap = true
        metaInfo = true
    }
}

if (org.gradle.internal.os.OperatingSystem.current().isMacOsX()) {
    task iosTest {
        def device = project.findProperty("iosDevice")?.toString() ?: "iPhone 8"
        dependsOn 'linkTestDebugExecutableIosx64'
        group = JavaBasePlugin.VERIFICATION_GROUP
        description = "Runs tests for target 'ios' on an iOS simulator"

        doLast {
            def binary = kotlin.targets.iosx64.compilations.test.getBinary('EXECUTABLE', 'DEBUG')
            exec {
                commandLine 'xcrun', 'simctl', 'spawn', device, binary.absolutePath
            }
        }
    }
    tasks.check.dependsOn(tasks.iosTest)
}


apply from: rootProject.file("gradle/publish.gradle")
