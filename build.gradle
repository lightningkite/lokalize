buildscript {
    ext.kotlinVersion = '1.3.10'
    repositories {
        mavenLocal()
        jcenter()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-dev' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
    }
}
apply plugin: 'kotlin-multiplatform'


repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
}
group 'com.lightningkite'
version '0.0.2'

apply plugin: 'maven-publish'
apply plugin: 'kotlin-multiplatform'

kotlin {
    targets {
        fromPreset(presets.jvm, 'jvm')
        fromPreset(presets.js, 'js')
        if (org.gradle.internal.os.OperatingSystem.current().isMacOsX()) {
            fromPreset(presets.iosX64, 'iosx64')
            fromPreset(presets.iosArm64, 'ios')
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                api 'org.jetbrains.kotlin:kotlin-stdlib-common'
                api 'com.lightningkite:kommon-metadata:0.0.2'
            }
        }
        commonTest {
            dependencies {
                api 'org.jetbrains.kotlin:kotlin-test-common'
                api 'org.jetbrains.kotlin:kotlin-test-annotations-common'
            }
        }
        jvmMain {
            dependencies {
                api 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
                api 'com.lightningkite:kommon-jvm:0.0.2'
            }
        }
        jvmTest {
            dependencies {
                api 'org.jetbrains.kotlin:kotlin-test'
                api 'org.jetbrains.kotlin:kotlin-test-junit'
            }
        }
        jsMain {
            dependencies {
                api 'org.jetbrains.kotlin:kotlin-stdlib-js'
                api 'com.lightningkite:kommon-js:0.0.2'
            }
        }
        jsTest {
            dependencies {
                api 'org.jetbrains.kotlin:kotlin-test-js'
            }
        }

        iosMain {
            dependencies {
                api 'com.lightningkite:kommon-ios:0.0.2'
            }
        }
        iosTest {
            dependencies {
            }
        }
        iosx64Main {
            dependencies {
                api "com.lightningkite:kommon-iosx64:${project.version}"
            }
        }
        iosx64Test {
            dependencies {
            }
        }
    }
}

tasks.withType(compileKotlinJs.getClass()) {
    kotlinOptions {
        moduleKind = "umd"
        sourceMap = true
        metaInfo = true
    }
}

if (org.gradle.internal.os.OperatingSystem.current().isMacOsX()) {
    task iosTest {
        def device = project.findProperty("iosDevice")?.toString() ?: "iPhone 8"
        dependsOn 'linkTestDebugExecutableIosx64'
        group = JavaBasePlugin.VERIFICATION_GROUP
        description = "Runs tests for target 'ios' on an iOS simulator"

        doLast {
            def binary = kotlin.targets.iosx64.compilations.test.getBinary('EXECUTABLE', 'DEBUG')
            exec {
                commandLine 'xcrun', 'simctl', 'spawn', device, binary.absolutePath
            }
        }
    }
    tasks.check.dependsOn(tasks.iosTest)
}